/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'application'

mainClassName = 'com.twosigma.beakerx.sql.kernel.SQL'

configurations {
  runtimetools
}

repositories {
  mavenCentral()
  maven {
    url 'http://redshift-maven-repository.s3-website-us-east-1.amazonaws.com/release'
  }
}

dependencies {
  compile group: "com.twosigma", name: "beakerx-kernel-base", version: "$beakerxVersion"
  compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.9.13'
  compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.1'
  //jdbc drivers
  compile group: 'com.h2database', name: 'h2', version: '1.4.197'
  compile group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '6.2.2.jre8'
  compile group: 'postgresql', name: 'postgresql', version: '9.1-901-1.jdbc4'
  compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.18'
  compile group: 'org.apache.derby', name: 'derby', version: '10.14.1.0'
  compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
  compile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'

  runtimetools group: "com.twosigma", name: "beakerx-runtimetools", version: "$beakerxVersion"

  testCompile group: "com.twosigma", name: "beakerx-kernel-base-test", version: "$beakerxVersion"
}

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }
}
distZip.dependsOn installDist

task copyDependencies(type: Copy) {
  from configurations.runtime
  from jar
  into new File('../sql-dist/beakerx_sql/kernel/', 'lib')
}
task copyStaticContent(type: Copy) {
  from '.'
  into new File('../sql-dist/beakerx_sql/kernel/', '')
  include '*.png'
  include 'kernel.json'
}

jar.finalizedBy copyDependencies
jar.finalizedBy copyStaticContent

task copyRuntimetools(type: Copy) {
  from configurations.runtimetools
  from jar
  into new File('../sql-dist/beakerx_sql/kernel/', 'ext')
  include 'beakerx-runtimetools*.jar'
}
jar.finalizedBy copyRuntimetools
